// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CalculatePressure
#pragma kernel CalculateForces
#pragma kernel UpdateParticlePositions

//Shaders really like structures divisible by 128 bits, that's why our struct has dummy variables.
//https://developer.nvidia.com/content/understanding-structured-buffer-performance
//https://developer.nvidia.com/content/redundancy-and-latency-structured-buffer-use

//Each float in HLSL is 32bits (4bytes)
//https://docs.microsoft.com/en-us/windows/win32/direct3dhlsl/dx-graphics-hlsl-scalar

struct particle{
    float3 position;
    float density;
    float pressure;
    float3 forces;
    float3 velocity;
    float3 pad0;
    float pad1;
    float pad2;
};

// RandomWrite Structured Buffer
RWStructuredBuffer<particle> particleBuffer;
uniform float h;
uniform float gasConstant;
uniform float restDensity;
uniform float viscosityConstant;
uniform float deltaTime;
uniform uint particleCount;
uniform float3 bounds;
uniform float3 externalForcePoint;
uniform float externalForceMagnitude;
uniform float particleMass;
uniform float poly6Constant;
uniform float spikyConstant;
uniform float laplaceConstant;
uniform float3 gravity;

float Poly6Kernel(float rSqrd, float h){
    const float hSqrd = h * h;
    //0 <= r && r <= h condition from Müller, M., Charypar, D,. & Gross, M. (2003) Eq. 20 enforced by step functions.
    return particleMass * poly6Constant * pow(hSqrd - rSqrd, 3) * step(0, rSqrd) * step(rSqrd, hSqrd);
}

// Spiky kernel smoothing function (Eq. 21).
float SpikyKernel(float r, float h){
    float hr = h - r;
    return spikyConstant * hr * hr * hr * step(0, r) * step(r, h);
}

// Laplacian of the viscosity kernel (Eq. 22)
float ViscosityKernel(float r, float h){
    return laplaceConstant * (h-r) * step(0, r) * step(r, h);
}

[numthreads(1024,1,1)]
void CalculatePressure (uint3 id : SV_DispatchThreadID){
    //Poly6 smoothing kernel to approximate fluid density. Eq. 3
    //Müller M, Charypar D & Gross M. 2003
    float3 currentPos = particleBuffer[id.x].position;
    float density = 0;

    for(uint i = 0; i < particleCount; i++){
        const float3 otherPos = particleBuffer[i].position;
        const float3 diff = otherPos - currentPos;
        density += Poly6Kernel(dot(diff, diff), h);
    }

    //The pressure at the point of the particle can be calculated using Eq. 12 (Müller M, Charypar D & Gross M. 2003).
    particleBuffer[id.x].density = density;
    particleBuffer[id.x].pressure = gasConstant * (density - restDensity);
}

[numthreads(1024,1,1)]
void CalculateForces (uint3 id : SV_DispatchThreadID){
    const float h2 = h * h;
    const float h3 = h * h * h;

    const float3 currentPos = particleBuffer[id.x].position;
    float3 pressureForce = float3(0,0,0);
    float3 viscosityForce = float3(0,0,0);

    //There's also a lot of performance to be found here if we only check the neighboring particles.
    //Right now this is more or less O(n^2)
    for (uint i = 0; i < particleCount; i++){
        if(i == id.x)
            continue;

        const float3 otherPos = particleBuffer[i].position;
        const float3 diff = currentPos - otherPos;
        const float r2 = dot(diff, diff);
        const float r = length(diff);
        const float otherDensity = particleBuffer[i].density;

        //Calculate pressure force magnitude (Eq. 10), weighted by the spiky kernel. Force direcion by normalized diff.
        pressureForce -= particleMass * ((particleBuffer[id.x].pressure + particleBuffer[i].pressure) / (2 * otherDensity)) * SpikyKernel(r, h) * normalize(diff);
        //Calculating the viscosity force (Eq. 14), weighted by the laplacian of the viscosity kernel.
        viscosityForce += particleMass * (particleBuffer[i].velocity - particleBuffer[id.x].velocity) / otherDensity * ViscosityKernel(r, h);
    }
    viscosityForce *= viscosityConstant;
    //Calculate resulting force.
    particleBuffer[id.x].forces += pressureForce + viscosityForce + gravity;

    //Include force towards mouse cursor if clicked.
    const float3 diff = externalForcePoint - particleBuffer[id.x].position;
    const float rSqrd = dot(diff, diff);
    //Using smoothstep to smoothly reduce the force acting on each particle as a function of the distance between the particle and the cursor.
    particleBuffer[id.x].forces += (1.f-smoothstep(20.0f, 30.0f, rSqrd)) * (externalForcePoint - particleBuffer[id.x].position) * externalForceMagnitude;
}

[numthreads(1024,1,1)]
void UpdateParticlePositions (uint3 id : SV_DispatchThreadID){
    //Semi-implicit Euler integration.
    particleBuffer[id.x].velocity += (particleBuffer[id.x].forces / particleBuffer[id.x].density) * deltaTime;
    particleBuffer[id.x].position += particleBuffer[id.x].velocity * deltaTime;

    //As a result of our bounding box being axis aligned, particle/bound collision is trivial to detect and correct.
    if(particleBuffer[id.x].position.x + h > bounds.x){
        particleBuffer[id.x].position.x = bounds.x - h;
        particleBuffer[id.x].velocity.x *= -0.3;
    }
    if(particleBuffer[id.x].position.x - h < -bounds.x){
        particleBuffer[id.x].position.x = -bounds.x + h;
        particleBuffer[id.x].velocity.x *= -0.3;
    }

    if(particleBuffer[id.x].position.z + h > bounds.z){
        particleBuffer[id.x].position.z = bounds.z - h;
        particleBuffer[id.x].velocity.z *= -0.3;
    }
    if(particleBuffer[id.x].position.z - h < -bounds.z){
        particleBuffer[id.x].position.z = -bounds.z + h;
        particleBuffer[id.x].velocity.z *= -0.3;
    }

    if (particleBuffer[id.x].position.y - h < 0){
        particleBuffer[id.x].position.y = h;
        particleBuffer[id.x].velocity.y *= -0.3;
    }

    particleBuffer[id.x].forces = float3(0,0,0);
}